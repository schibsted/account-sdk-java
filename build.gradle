plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
}

version '1.0.2'
group 'com.schibsted.account'

repositories {
    mavenCentral()
}

dependencies {
    compile('com.konghq:unirest-java:3.10.00')
    compile('com.nimbusds:oauth2-oidc-sdk:8.19')
    compile('ch.qos.logback:logback-core:1.2.3')
    compile('ch.qos.logback:logback-classic:1.2.3')
    compile('org.slf4j:slf4j-api:1.7.25')

    testCompile('junit:junit:4.12')
    testCompile('org.mockito:mockito-core:2.+')
}

java {
    withJavadocJar()
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

test {
    beforeTest { descriptor ->
        logger.lifecycle("Running test: " + descriptor)
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = 'Schibsted account SDK Java'
                packaging = 'jar'
                description = "Schibsted account Java SDK ${project.version}"
                url = 'https://github.com/schibsted/account-sdk-java'

                scm {
                    connection = 'scm:git:https://github.com/schibsted/account-sdk-java.git'
                    url = 'https://github.com/schibsted/account-sdk-java'
                }

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id = 'schibsted-account'
                        name = 'Schibsted account team'
                        email = 'schibstedaccount@schibsted.com'
                    }
                }
            }
        }
    }
}

nexusPublishing {
    repositories {
        sonatype()
    }
}

signing {
    useInMemoryPgpKeys(new String(System.getenv('SIGNING_KEY').decodeBase64()), System.getenv('SIGNING_PASSWORD'))
    sign publishing.publications.mavenJava
}
